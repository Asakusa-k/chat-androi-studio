buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
        maven { url "https://jitpack.io" }
        maven { url "https://dl.bintray.com/uephon/maven/" }
        maven { url "https://api.xposed.info/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
//apply plugin: 'com.android.dynamic-feature'

android {
    namespace 'com.android.launcher3'
    compileSdkVersion 33
    dynamicFeatures = []
    buildFeatures{aidl = true}
    defaultConfig {
        minSdkVersion 14  // Minimum for legacy-support-v4, adjust if needed
        targetSdkVersion 27
        versionCode 30907
        versionName "3.9"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }

        optimized {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
        }
    }

    flavorDimensions 'main'

    productFlavors {
        aosp {
            // Remove applicationId - not needed for library modules
            // Remove testApplicationId - not needed for library modules
            dimension 'main'
        }

        // Add fdroid flavor to match app module
        fdroid {
            dimension 'main'
        }

        l3go {
            // Remove applicationId - not needed for library modules
            // Remove testApplicationId - not needed for library modules
            dimension 'main'
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            aidl.srcDirs = ['src/main','src']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
                srcDir 'proto_pixel/'
            }
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        aosp {
            java.srcDirs = ['src_flags']
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTestAosp {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src_flags']
            // Note: we are using the Launcher3 manifest here because the gradle manifest-merger uses
            // different attributes than the build system.
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTestL3go {
            manifest.srcFile "tests/AndroidManifest.xml"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

final String SUPPORT_LIBS_VERSION = '27.1.1'
dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.android.support:support-v4:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-dynamic-animation:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:palette-v7:${SUPPORT_LIBS_VERSION}"
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.2.0rc2'
    implementation 'androidx.annotation:annotation:1.9.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_LIBS_VERSION}"
}
protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_log.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_dump.proto|com.android.launcher3.model.nano"
                        option "java_package=search.proto|com.google.android.apps.nexuslauncher.search.nano"
                        option "java_package=smartspace.proto|com.google.android.apps.nexuslauncher.smartspace.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}
